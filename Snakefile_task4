configfile: "task4.yaml"

years= sorted(config["years"])
make_compare = years ==[2019,2024]

rule all:
    input:
        expand("results/pm25/{year}/exceedance_days.csv", year=years),
        expand("results/pm25/{year}/daily_means.csv", year=years),
        expand("results/literature/{year}/pubmed_papers.csv",year=years),
        "results/literature/compare.png" if make_compare else [],
        "results/report_task4.md"

rule report_task4_year:
    input:
        top="results/literature/{year}/top_journals.csv",
        pm25="results/pm25/{year}/exceedance_days.csv",
        papers="results/literature/{year}/pubmed_papers.csv",
        year_summary="results/literature/{year}/summary_by_year.csv",
        top_words="results/literature/{year}/top_words.csv"
    output:
        report="results/report_task4_{year}.md"
    run:
        import pandas as pd
        year = wildcards.year

        with open(output.report, "w", encoding="utf-8") as f:
            f.write(f"# Raport PubMed {year}\n\n")

            # PM2.5
            df = pd.read_csv(input.pm25, skiprows=[2])
            f.write(f"## Dni z przekroczeniem normy {year}\n\n")
            f.write(df.to_markdown(index=False) + "\n\n")

            # Liczba publikacji dla zapytania
            df_pub = pd.read_csv(input.papers)
            f.write(f"## Liczba publikacji {year}\n\n")
            f.write(f"{df_pub.shape[0]}\n\n")

            # Trend liczby publikacji w czasie
            df_time=pd.read_csv(input.year_summary)
            f.write(f"## Trend liczby publikacji w czasie\n\n")
            f.write(df_time.to_markdown(index=False) + "\n\n")

            # Top journals
            df_top = pd.read_csv(input.top)
            f.write(f"## Top 10 Journals {year}\n\n")
            f.write(df_top.to_markdown(index=False) + "\n\n")

            # Przykładowe tytuły
            example_titles=df_pub['Tytuł'].drop_duplicates().head(5)
            f.write("## Przykładowe tytuły publikacji\n\n")
            for title in example_titles:
                f.write(f"-{title}\n\n")
            f.write("\n")

            # Top słowa
            df_words=pd.read_csv(input.top_words)
            f.write(f"## Top słowa {year}\n\n")
            f.write(df_words.to_markdown(index=False)+ "\n\n")

if make_compare:
    rule compare_top_words:
        input:
            y1="results/literature/2019/top_words.csv",
            y2="results/literature/2024/top_words.csv"
        output:
            plot="results/literature/compare.png"
        
        run:
            import pandas as pd
            import matplotlib
            matplotlib.use("Agg")
            import matplotlib.pyplot as plt

            df_2019=pd.read_csv(input.y1)
            df_2024=pd.read_csv(input.y2)

            fig, ax = plt.subplots(figsize=(8,5))

            ax.barh(df_2019["Słowo"],df_2019["Liczba"], label="2019")
            ax.barh(df_2024["Słowo"],df_2024["Liczba"], label="2024")

            ax.set_title("Top słowa w tytułach publikacji")
            ax.set_xlabel("Liczba wystąpień")
            ax.legend()

            plt.savefig(output.plot)
            plt.close()


rule report_task4:
    input:
        years_reports=expand("results/report_task4_{year}.md", year=years),
        compare_plot=("results/literature/compare.png" if make_compare else [])
    output:
        "results/report_task4.md"
    run:
        # scalamy raporty roczne
        with open(output[0], "w", encoding="utf-8") as f_out:
            for file in input.years_reports:
                with open(file, "r", encoding="utf-8") as f_in:
                    f_out.write(f_in.read() + "\n\n")
            if make_compare:
                # dodajemy wykres tylko w końcowym raporcie
                f_out.write("## Porównanie lat\n\n")
                f_out.write(f"![Top słowa](literature/compare.png)\n\n")


rule pubmed_year:
    input:
        script="src/literature/pubmed_fetch.py"
    output:
        papers="results/literature/{year}/pubmed_papers.csv",
        top="results/literature/{year}/top_journals.csv",
        summary="results/literature/{year}/summary_by_year.csv",
        top_words="results/literature/{year}/top_words.csv"
    shell:
        "python {input.script} --year {wildcards.year} --config task4.yaml"


rule pm25_year:
    input:
        data="data/pm25/{year}/df{year}.csv",
        script="src/pm25/Analysis.py"
    output:
        exceeed="results/pm25/{year}/exceedance_days.csv",
        mean="results/pm25/{year}/daily_means.csv"
    params:
        pm25_norm=config["pm25_norm"]
    shell:
        "python {input.script} {input.data} {params.pm25_norm} {output.exceeed} {output.mean}"


rule pm25:
    input:
        script="src/pm25/DownloadClean.py"
    output:
        "data/pm25/{year}/df{year}.csv"
    shell:
        "python {input.script} {output} {wildcards.year}"
